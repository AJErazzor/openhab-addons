grammar org.openhab.model.Sitemap with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate sitemap "http://www.openhab.org/model/Sitemap"

SitemapModel :
	'sitemap' Sitemap;

Sitemap :
		name=ID ('label=' label=STRING)? ('icon=' icon=STRING)? '{'
			(children+=Widget)+
		'}';

Widget :
	(LinkableWidget | NonLinkableWidget);	

NonLinkableWidget :
	Switch | Selection | Slider | List | Setpoint | Video | Chart | Webview | Colorpicker
;

LinkableWidget :
	(Text | Group | Image | Frame)
	('{'
			(children+=Widget)+
	'}')?;

Frame : {Frame} 'Frame' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Text : 
	{Text} 'Text' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Group : 
	'Group' (('item=' item=GroupItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Image : 
	'Image' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('url=' url=(STRING)) ('refresh=' refresh=INT)? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Video : 
	'Video' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('url=' url=(STRING)) & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Chart : 
	'Chart' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('service=' service=(STRING))? & ('refresh=' refresh=INT)? & ('period=' period=ID) & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Webview : 
	'Webview' (('item=' item=ItemRef)? & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('url=' url=(STRING)) & ('height=' height=INT)? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);
	
Switch : 
	'Switch' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('mappings=[' mappings+=Mapping (',' mappings+=Mapping)* ']')? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);
	
Slider :
	'Slider' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('sendFrequency=' frequency=INT)? & (switchEnabled?='switchSupport')? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);
	
Selection : 
	'Selection' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('mappings=[' mappings+=Mapping (',' mappings+=Mapping)* ']')? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

List : 
	'List' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? ('separator=' separator=STRING) & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Setpoint : 
	'Setpoint' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('minValue=' minValue=Number)? & ('maxValue=' maxValue=Number)? & ('step=' step=Number)? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Colorpicker : 
	'Colorpicker' (('item=' item=ItemRef) & ('label=' label=(ID|STRING))? & ('icon=' icon=Icon)? & ('sendFrequency=' frequency=INT)? & ('labelcolor=' LabelColor=STRING)? & ('valuecolor=' ValueColor=STRING)? & ('iconcolor=' IconColor=STRING)?);

Mapping :
	cmd=Command '=' label=(ID|STRING);

ItemRef :
	ID
;

GroupItemRef :
	ID
;

Icon :
	STRING
;

Command returns ecore::EString:
	INT | ID | STRING;

Number returns ecore::EBigDecimal:
	INT | FLOAT;


terminal ID : ('^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*) |
	 (('0'..'9')+ ('a'..'z'|'A'..'Z'|'_') ('0'..'9'|'a'..'z'|'A'..'Z'|'_')*);
	
terminal FLOAT returns ecore::EBigDecimal:
	INT '.' INT;
